apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "nl.tudelft.cs4160.trustchain_android"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 21
        versionName "0.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude '**/MessageProto.java'
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    useLibrary 'android.test.runner'
}

dependencies {
    def room_version = "2.2.2"
    def lifecycle_version = "2.1.0"
    def dagger_version = "2.24"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':simpletorrentandroid')

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.rmtheis:tess-two:9.0.0'
    implementation 'org.jmrtd:jmrtd:0.5.5'
    implementation 'com.madgag.spongycastle:core:1.54.0.0'
    implementation 'com.madgag.spongycastle:prov:1.54.0.0'
    implementation 'net.sf.scuba:scuba-sc-android:0.0.9'
    implementation 'com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:1.0.7'

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // Local unit tests
    testImplementation group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.25.1'

    // Instrumented tests
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:1.0.7'
    androidTestImplementation 'org.mockito:mockito-core:2.25.1'
    androidTestImplementation 'org.mockito:mockito-android:2.25.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}
